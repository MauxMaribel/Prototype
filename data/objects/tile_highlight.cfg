{
	id: "tile_highlight",
	is_strict: true,

	properties: {
		loc: { type: "Loc" },

		render: "def() ->commands
		set(animation, {
			id: 'fbo',
			image: 'fbo',
			x: 0, y: 0, w: tile_dim, h: tile_dim,
			scale: 1,
			frames: 1,
			fbo: c.render(tile_dim, tile_dim, [
				c.set_source_color('white'),
				c.rectangle(0, 0, tile_dim, tile_dim),
				c.fill(),
			])
		})
			where c = canvas()
		",

		handle_click: { type: "null|function(Loc)->commands" },

		tile_dim: "int :: 64",
	},

	events: {
		create: "render()",

		process: "set(alpha, 64 + int(sin(cycle*4)*40))",

		mouse_enter: "[
			set(green, 0),
			set(blue, 0),
		]",
		mouse_leave: "[
			set(green, 255),
			set(blue, 255),
		]",

		click: "if(handle_click != null, handle_click(loc))",
	},
}
