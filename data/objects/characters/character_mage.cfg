{
	id: "character_mage",
	is_strict: true,

	//say that this object should be available in the editor, and give
	//a category for it to go in.
	editor_info: {
		category: "player",
	},

	properties: {
	},

	events: {
		"+end_walk_right_anim": "set(animation, 'walk_right')",

		//this event occurs every frame.
		process: "
		if(not animated_movements,
		//if the object isn't already walking somewhere (has an animation
		//going on) then see if a key is pressed which starts her walking
		[

			if(('up' in keys), [
				//make sure we're not flipping the sprite. (facing = 1
				//means no flipping is going on, facing = -1 flips her)
				set(facing, 1),

				//set the animation. (See the animations: section at the
				//bottom of this file for the possible animations).
				set(animation, 'stand_up'),

				//make her move 64 pixels upwards over 15 frames.
				animate(me, {
					mid_y: mid_y - 64,
				}, {
					duration: 15,
					easing: lib.math.linear,
				})
			],

			'down' in keys, [
				set(facing, 1),
				set(animation, 'stand_down'),
				animate(me, {
					mid_y: mid_y + 64,
				}, {
					duration: 15,
					easing: lib.math.linear,
				})
			],

			'left' in keys, [
				set(facing, -1),
				if(animation != 'walk_right', set(animation, 'walk_right')),
				animate(me, {
					mid_x: mid_x - 64,
				}, {
					duration: 15,
					easing: lib.math.linear,
				})
			],

			 'right' in keys, [
				set(facing, 1),
				if(animation != 'walk_right', set(animation, 'walk_right')),
				animate(me, {
					mid_x: mid_x + 64,
				}, {
					duration: 15,
					easing: lib.math.linear,
				})
			],
			if(animation = 'walk_right', set(animation, 'stand_right'))
			),
		]

		where keys = map(level.player.ctrl_keys, lower(value))
		)
		",
	},

	//this defines all the animations the character has in her spritesheet.
	animation: [

	{
		id: "stand_down",
		image: "sprites/mainctilesheet.png",
		x: 1, y: 1, w: 31, h: 31,
		frames: 1,
		pad: 1,
		duration: -1,
		scale: 4,
	},

	{
		id: "stand_up",
		image: "sprites/mainctilesheet.png",
		x: 33, y: 1, w: 31, h: 31,
		frames: 1,
		pad: 1,
		duration: -1,
		scale: 4,
	},

	{
		id: "stand_right",
		image: "sprites/mainctilesheet.png",
		x: 65, y: 1, w: 31, h: 31,
		frames: 1,
		pad: 1,
		duration: -1,
		scale: 4,
	},

	{
		id: "walk_right",
		image: "sprites/mainctilesheet.png",
		x: 65, y: 1, w: 31, h: 31,
		frames: 3,
		pad: 1,
		duration: 5,
		reverse: true,
		scale: 4,
	},

	],
}
