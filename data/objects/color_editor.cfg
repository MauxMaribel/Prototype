{
	id: "color_editor",
	is_strict: true,
	always_active: true,

	properties: {
		_width: "int :: 140",
		_height: "int :: 84 + palette_pad",

		palette_pad: "int :: if(palette, 30, 0)",

		current_values: "[decimal,decimal,decimal] :: _get_slider_values",

		_initial_values: { type: "[decimal,decimal,decimal]", default: [1.0, 1.0, 1.0] },

		palette: { type: "[[decimal,decimal,decimal]]", default: [] },

		palette_area: "def(int nitem) ->[int,int,int,int] [10 + nitem*24, 10, 16, 16]",
		palette_area_selected: "def(int xpos, int ypos) ->null|int
		find(range(size(palette)), xpos >= area.x and ypos >= area.y and xpos < area.x+area[2] and ypos < area.y+area[3] where area = palette_area(index))
		",

		_sliders: { type: "[obj slider_widget]", default: [] },

		_get_slider_values: "[decimal,decimal,decimal] <-
			map(_sliders, value.slider_value)
		",

		_get_color: "[decimal,decimal,decimal] ::
			lib.color.hsv_to_rgb(_get_slider_values)
		",

		on_change: { type: "null|function()->commands" },

		on_update_values: "def(decimal new_value) ->commands
		[
			render(),
			if(on_change, on_change()),
		]
		",

		render: "def() ->commands
				set(animation, {
				id: 'svg',
				image: 'svg',
				fbo: c.render(_width, _height, [
					c.set_source_rgba(0.3, 0.3, 0.3, 1),
					c.rectangle(0, 0, _width, _height),
					c.fill(),

					if(palette, [
						map(palette, [
							c.set_source_color(lib.color.hsv_to_rgb(value)),
							c.rectangle(area[0], area[1], area[2], area[3]),
							c.fill(),

							c.set_source_color([0.7, 0.7, 0.7, 1.0]),
							c.set_line_width(1),
							c.rectangle(area[0], area[1], area[2], area[3]),
							c.stroke(),
						] where area = palette_area(index)),
						c.translate(0, 30),
					]),

					map(range(3), [
						map(range(num_divisions),

						[
							c.rectangle(rect_xpos, rect_ypos, 1, rect_height),
							c.set_source_color(lib.color.hsv_to_rgb(pixel_hsv)),
							c.fill(),
						]

						where pixel_hsv = [decimal,decimal,decimal]<- (hsv[:component] + [decimal(index)/decimal(num_divisions)] + hsv[component+1:])

						where rect_xpos = 10 + (decimal(index)/decimal(num_divisions))*(_width - 20)
						),
					]
					where component = value
					where rect_ypos = 10 + index*24 + 6
					where rect_height = 4
					where num_divisions = _width - 20
					),
				])
				where color = _get_color
				where hsv = _get_slider_values
				where c = canvas(),
				x: 0,
				y: 0,
				w: _width,
				h: _height,
				scale: 1.0,
				frames: 1,
				duration: -1,
			})
		",

		_create_sliders: "def() ->commands
		[
			map(_sliders, remove_object(value)),
			set(_sliders, []),

			map(range(3),
				spawn('slider_widget', {
					x: x + 10,
					y: y + palette_pad + 10 + index*24,
					slider_width: _width-20,
					slider_height: 16,
					zorder: zorder - 1,
					value_change_handler: on_update_values,
					slider_value: _initial_values[index],
				}, [
					add(_sliders, [child]),
				])
			),
		]
		",
	},

	events: {
		create: "[

			_create_sliders();
			render()
		]",

		being_removed: "[
			map(_sliders, remove_object(value))
		]",

		type_updated: "fire_event('being_removed'); fire_event('create')",

		click: "
		if(palette_item != null,
			set(_initial_values, palette[palette_item]);
			_create_sliders();
			render();
			if(on_change, on_change())
		)
		where palette_item = palette_area_selected(xpos, ypos)	
		where xpos = arg.mouse_x - x
		where ypos = arg.mouse_y - y
		",
	},
}
